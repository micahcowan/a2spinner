5 DIM B$(40)
10 PRINT "HELLO.":PRINT "MY NAME IS APPLE ][."
20 PRINT "WHAT'S YOUR NAME";:INPUT A$:L=LEN(A$)
25 FOR P=1 TO 250:NEXT
30 PRINT:PRINT "THANK YOU, ";
40 FOR I=1 TO L:B$(I)=MID$(A$,I,1):NEXT I
50 FOR I=L TO 1 STEP -1:PRINT B$(I);:NEXT I
55 GET K$
60 PRINT ".":PRINT "OOPS! I GUESS I GOT IT BACKWARDS."
65 PRINT "A SMART COMPUTER LIKE ME SHOULDN'T"
70 PRINT "MAKE A MISTAKE LIKE THAT!":PRINT
75 GET K$
80 PRINT "BUT I JUST NOTICED YOUR LETTERS ARE"
85 PRINT "OUT OF ORDER. LET'S PUT THEM IN ORDER"
90 PRINT "LIKE THIS: ";
100 FOR J=2 TO L: I=J-1: T$=B$(J)
110 IF T$>B$(I) THEN 130
120 B$(I+1)=B$(I): I=I-1:IF I>0 THEN 110
130 B$(I+1)=T$:NEXT J
140 FOR I=1 TO L:PRINT B$(I);:NEXT I:PRINT:PRINT
145 FOR P=1 TO 800:NEXT
150 PRINT "DON'T YOU LIKE THAT BETTER";:INPUT D$
160 IF LEFT$(D$,1)="Y" THEN 180
170 PRINT:PRINT "I'M SORRY YOU DON'T LIKE IT THAT WAY.":GOTO 200
180 PRINT:PRINT "I KNEW YOU'D AGREE!!"
200 PRINT:PRINT "I REALLY ENJOYED MEETING YOU, ";A$;"."
210 PRINT "HAVE A NICE DAY!"
999 END
